resource "aws_s3_bucket" "this" {
  bucket              = var.bucket_name
  object_lock_enabled = true

  tags = merge(var.common_tags, {
    Name    = var.bucket_name
    Purpose = "youform-signatures"
  })
}


resource "aws_s3_bucket_versioning" "this" {
  bucket = aws_s3_bucket.this.id

  versioning_configuration {
    status = "Enabled"
  }
}


resource "aws_s3_bucket_object_lock_configuration" "this" {
  bucket = aws_s3_bucket.this.id

  rule {
    default_retention {
      mode  = "COMPLIANCE"
      years = var.object_lock_retention_years
    }
  }
}


resource "aws_s3_bucket_server_side_encryption_configuration" "this" {
  bucket = aws_s3_bucket.this.id

  rule {
    apply_server_side_encryption_by_default {
      sse_algorithm = "AES256"
    }
  }
}


resource "aws_s3_bucket_public_access_block" "this" {
  bucket = aws_s3_bucket.this.id

  block_public_acls       = true
  block_public_policy     = true
  ignore_public_acls      = true
  restrict_public_buckets = true
}


resource "aws_s3_bucket_policy" "protect_signatures" {
  bucket = aws_s3_bucket.this.id

  policy = jsonencode({
    Version = "2012-10-17"
    Statement = [
      {
        Sid       = "DenyObjectDeletes"
        Effect    = "Deny"
        Principal = "*"
        Action = [
          "s3:DeleteObject",
          "s3:DeleteObjectVersion"
        ]
        Resource = [
          "${aws_s3_bucket.this.arn}/*"
        ]
      },
      {
        Sid       = "DenyLifecycleChanges"
        Effect    = "Deny"
        Principal = "*"
        Action = [
          "s3:PutLifecycleConfiguration",
          "s3:DeleteBucketLifecycle"
        ]
        Resource = aws_s3_bucket.this.arn
      }
    ]
  })
}
